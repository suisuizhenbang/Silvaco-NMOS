go athena

#定义网格
line x loc=0 spac=0.1
line x loc=0.2 spac=0.006
line x loc=0.65 spac=0.006
line y loc=0.00 spac=0.002
line y loc=0.2 spac=0.005
line y loc=0.5 spac=0.05
line y loc=1.0 spac=0.15

#定义初始Si
init silicon c.boron=1e14 orientation=100 space.mul=2 two.d

#制作栅氧化层
diffus time=10 temp=950 dryo2 press=1.00 hcl.pc=3

#抽取氧化层厚度
extract name="gateox" thickness oxide mat.occno=1 x.val=0.3

#调整阈值电压，注入硼离子
implant boron dose=2e12 energy=10 pearson

#淀积Poly层
depo poly thick=0.25 divi=10

#栅刻蚀
etch poly left p1.x=0.35

#湿氧氧化
diffuse time=3 temp=900 weto2

#轻掺杂离子注入
implant phosphor dose=3e13 energy=20 tilt=0 rotation=0

#沉积氧化层再进行干刻蚀
depo oxide thick=0.120 divisions=8
etch oxide dry thick=0.120

#进行源漏砷离子的注入
implant arsenic dose=4.0e15 energy=40 tilt=0 rotation=0
 
#快速退火
method fermi 
diffus time=1 temp=900 nitro press=1.00 

etch oxide left p1.x=0.1
etch oxide start x=0.357 y=-0.15
etch cont x=0.65 y=-0.15 
etch cont x=0.65 y=-0.3 
etch done x=0.357 y=-0.3 

#沉积铝
deposit alumin thick=0.1 div=20 

#刻蚀铝
etch alumin start x=0.35 y=0.1
etch cont x=0.1 y=0.1
etch cont x=0.1 y=-0.4
etch done x=0.35 y=-0.4

tonyplot
quit